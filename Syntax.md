
**[Impromptu's Syntax](#requirements_&_installation)**
- [Language](#language)
- [Prompt](#prompt)
    - [Snippets](#snippets)
- [Composer](#composer)
- [Chain](#chain)
- [Imports](#imports)
-----------
## Syntax
The core of Impromptu is its semantics that allow to wirte a propt as a programming fuction using variables and calls to another prompts.


A `.prm` file contains a **Language**, and a set of **prompts**, **chain** and **composer** (Assets). It may also contain one or more **imports** from other `.prm` files.

### `Language`

Every `.prm` starts by declaring the language of the prompt that will generate. The grammar of it is:
```
language = <language>
```

Until now, the two possible languages are Spanish and English (i.e `language=English`).
This **element is optional**. If it was not declared, **it is assumed that the language is English**.
> *In the future, it will be used to ensure the compatibility between an asset and its imports/references*.

### `Prompt`

`Prompt` is the basic function (asset) that generates a prompt. It is formed by several elements that allow to express the concepts that the generated prompt has to fulfill:

**name**: name that we give to the prompt. It has to be unique, and serves to reference the prompt in other places

**inputs**: Parameters and another inputs that are used to generate the prompt. There are two types of inputs:
- **parameter**: Transmit a `snippet` (explained in [the following subsection](#snippets)) inside the `prompt` as a variable. They should start by an `@` symbol.
- **metadata**: *to be developed*

The inputs can be **together with a description** of it, that helps to understand the usage and meaning of that varaible

**media**: Indicate the types of exit the prompt will generate when used in the LLM.

**prefix**, **core** & **suffix**: Therese arelemts are what will truly generate the content of the prompt. They are composed by a set of commands (called `snippets`) that induces that statement in the prompt. `snippets` will be explained in [the next section](#snippets). 

> The difference betweeen prefix, core and suffix is merely conceptual at the moment. The snippets in the core are supposed to be related with the intructions of the prompt related to generate the answer, while in suffix and prefix appear instructions to guide the LLM to a correct and more specific answer.

**separator**: Tells how the different `snippets` are linked to each other. It was assigned a default separator for each LLM. For example, for ChatGPT, the default separator is `". "`.

**language**: A prompt can declare its language individually of the language of the `.prm` is in.

Its grammar is:

```
prompt <name> ([<inputs>]): <media>
[prefix = <snippets>]
core = <snippets>
[suffix = <snippets>]
[separator = <string>]
[language = <language>]

```


#### `Snippets`

The snippets are the core of Impromptu. They are the "parts" that represent the different ideas that the generated prompt is wanted to have. There are 4 types of snippets:

1. **Text plain.** Just write a text that you want to be in the prompt

2. **Input.** Obviously, the inputs transmitted to the prompt can be referenced and be used it. 

3. **Another asset**. You can use another asset from the same file (or that it were imported). For example, if you want to create a prompt to generate a poem about friendship, you may add a provious asset `generate_poem()` that generates a promptthat will create a poem.

4. **Traits**. The most useful type of snippet are the traits. They work as reference to assets, but about general topics that usually appear in a well-formed prompt. For example, if in the previous example we want to ensure that the generated poem in the previos example is for children, we should add `audience("children")`. The whole list of the implemented traits, plus their options are documented in [this document](traits_cheat_sheet.md).

Let see an example of how the snippets work. 
Imagine we want to generate prompts that allow us to generate pictures of fusion between animals. Specifically, we want to generate a gryphon (mythological beast half lion half eagle):
```
composer GenerateGryphoon(): image
  Mixture("eagle", "lion")

prompt Mixture(@animal1, @animal2): image
  core = audience( "children" ) weight high, 
         medium(drawing), 
         between(@animal1, @animal2), 
         no("violent"), no("scary")
```
The asset `Mixture()` contains the combination trait (`between()`) that express the idea of mixing two element, plus other revelant snippets that indicate properties that are wanted in the outcome. **It is important to remark the utility of the traits**. The traits simplify the writing of ideas that may need one or more lines of text to be expressed.

On the other hand, the prompt generated by `GenerateGhyphoon()` contains all the snippets from `Mixture(@animal1, @animal2)`, considering `@animal1 = eagle` and `@animal2 = lion`.


### `Composer`
A Composer is a structure that allows to merge several snippets together. The difference with a Prompt is that not media is declared because each part of the composer is considered as an independent prompt (*There is not any functional difference yet*).

```
composer <name> ([<inputs>]): 
<snippets>
[separator = <string>]
[language = <language>]

```

### `Chain`
*Not real utility right now*

### `Imports`

In addition to prompts, composer and chains, a `.prm`can also contain imports. The allow to use in a file a prompt declared in anothe file.

For example:

<i>ethic_questions.prm</i>
```
...
prompt job_statement(@ethical_concern,@job):  text
core = 
" a", @ethical_concern, "has to be a" , @job
separator=' '
language=English
...
```
*example.prm*
```
language=English

import job_statement from libraries.ethic_questions

prompt NewMain(): image
core=job_statement("black person", "doctor")
language=English
```
Resulted prompt:
> <i> a  black person  has to be a  doctor </i>


Similary to programming languages such as Java, the user can use other  elements **from a file located in `build_files`** by writing
```
import <name_1>,<name_2>,...,<name_n> from <file_location>
```
where `<name_i>` are the names of the asset imported, and `<file location>` is the relative path to the file from `build_files` (but without the `.prm` ending). For example, if the imported prompt is located in in the file `build_files/libraries/evaluation_questions.prm`, `<file location>` is `libraries.evaluation_questions`.
```
language=English

import job_statement,JSON_format from ethic_questions

prompt NewMain(): image
core=job_statement("black person", "doctor"),JSON_format()
language=English
```
In addition, the user also has the option to **import all the assets from a given file** by writing  `'*'` instaead of the name of all assets:

```
language=English

import * from ethic_questions

prompt NewMain(): image
core=job_statement("black person", "doctor"),JSON_format()
language=English
```
